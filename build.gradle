plugins {
	id 'org.springframework.boot' version "${springBootVersion}"
	id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
	id 'com.gorylenko.gradle-git-properties' version "${gitPropertiesPluginVersion}"

	id "java"
	id "idea"
	id "eclipse"
}

group = 'com.abelm'
version = '0.0.1-SNAPSHOT'
description = 'Prueba tecnica Banco Bisa'

repositories {
	mavenCentral()
	mavenLocal()
}

if (project.hasProperty("prod")) {
	apply from: "gradle/profile_prod.gradle"
} else {
	apply from: "gradle/profile_dev.gradle"
}

idea {
	module {
		excludeDirs += files("node_modules")
	}
}

eclipse {
	sourceSets {
		main {
			java {
				srcDirs += ["build/generated/sources/annotationProcessor/java/main"]
			}
		}
	}
}

defaultTasks "bootRun"

springBoot {
	mainClass = "com.abelm.blog_api.BlogApiApplication"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

gitProperties {
	failOnNoGitDirectory = false
	keys = ["git.branch", "git.commit.id.abbrev", "git.commit.id.describe"]
}

configurations {
	providedRuntime
	implementation.exclude module: "spring-boot-starter-tomcat"
}

dependencies {
	// Spring Boot
	implementation("org.springframework.boot:spring-boot-starter-web") {
		exclude module: "spring-boot-starter-tomcat"
		exclude module: "spring-web"
	}
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.2.5'
	implementation "org.springframework.boot:spring-boot-starter-undertow"
	implementation "org.springframework.boot:spring-boot-starter-actuator"
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
	// Zalando
	implementation 'org.zalando:problem-spring-web-starter:0.29.1'
	// H2 Database
	runtimeOnly 'com.h2database:h2'
	implementation "org.hibernate:hibernate-core:6.5.2.Final"
	implementation "org.hibernate.validator:hibernate-validator:8.0.1.Final"
	annotationProcessor "org.hibernate:hibernate-jpamodelgen:6.5.2.Final"
	implementation "io.hypersistence:hypersistence-utils-hibernate-63:3.8.1"
	implementation "com.fasterxml.jackson.core:jackson-databind:2.17.0"
	implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.0"
	implementation "com.fasterxml.jackson.datatype:jackson-datatype-hppc:2.17.0"
	implementation "com.fasterxml.jackson.datatype:jackson-datatype-hibernate6:2.17.0"
	// Apache Tools
	implementation "org.apache.commons:commons-lang3:3.14.0"
	// Lombok
	annotationProcessor 'org.projectlombok:lombok:1.18.32'
	compileOnly 'org.projectlombok:lombok:1.18.32'
	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

wrapper {
	gradleVersion = "8.8"
}

compileJava.dependsOn processResources

